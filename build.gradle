import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'idea'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.elsiff'
version = '3.0.3-SNAPSHOT'

ext {
    pluginName = 'MoreFish'
    author = 'elsiff'
    website = 'https://elsiff.me'
    mainPackage = 'me.elsiff.morefish'
    mainClass = mainPackage + '.MoreFish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://repo.dmulloy2.net/nexus/repository/public' }
    maven { url 'https://maven.enginehub.org/repo' }
    maven { url "https://repo.citizensnpcs.co" }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi' }
    maven { url 'https://nexus.neetgames.com/repository/maven-releases' }
    maven { url 'https://repo.aikar.co/content/groups/aikar' }
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup 'com.github.elsiff'
            includeGroup 'com.github.elsiff.egui'
            includeGroup 'com.github.MilkBowl'
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    compileOnly 'com.sk89q.worldguard:worldguard-legacy:6.2'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.29-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.gmail.nossr50.mcMMO:mcMMO:2.1.207'

    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'com.github.elsiff:egui:1.0.2-SNAPSHOT'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    relocate 'co.aikar.commands', "${mainPackage}.acf"
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "pluginName"     : pluginName,
            "project.version": project.property("version"),
            "author"         : author,
            "website"        : website,
            "mainClass"      : mainClass
    ]
}

build.dependsOn shadowJar